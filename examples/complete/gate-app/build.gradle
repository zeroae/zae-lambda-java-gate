plugins {
    id 'java'
}

ext.binDir = file("$buildDir/bin")
ext.gateCloudDir = file("$buildDir/gatecloud")
ext.gateApplicationDir = file("$gateCloudDir/application")

repositories {
    mavenCentral()
    /*
    maven {
        url 'https://maven.pkg.github.com/zeroae/packages'
        credentials {
            username = project.findProperty("github.username") ?: System.getenv("GITHUB_USER")
            password = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
     */
}

configurations {
    gate
}

dependencies {
    implementation project(":zae-lambda-java-gate")
    gate 'uk.ac.gate:gate-core:8.6.1'
}

// TODO: This is *not* currently working
//   - For some reason the plugins and resources are not being copied.
task packagegapp() {
    // This file is from gate-core
    // ref: https://gate.ac.uk/sale/tao/splitap5.html#sec:ant:packagegapp
    doLast {
        mkdir(gateApplicationDir)
        ant.typedef(resource: 'gate/util/ant/antlib.xml', classpath: configurations.gate.asPath)
        ant.packagegapp(
                src: 'application.xgapp',
                destfile: "$gateApplicationDir/application.xgapp",
                copyPlugins: 'yes',
                copyResourceDirs: 'yes',
                onUnresolved: 'fail'
        )
    }
}

task copyBin(type: Copy) {
    from "../wrapper.sh"
    into binDir

    fileMode 0755
}

task gatecloudUnpack(type: Copy) {
    def zipFile = file("application.zip")

    from zipTree(zipFile)
    into gateApplicationDir
}
sourceSets.main.output.dir gateCloudDir, builtBy: gatecloudUnpack
sourceSets.main.output.dir binDir, builtBy: copyBin
